use strict;
use warnings;
use Path::Tiny;
use Data::Dumper::Compact;
use List::UtilsBy qw(nsort_by);

my $data = eval '+'.($ARGV[0] =~ /^[\{\[]/ ? $ARGV[0] : path($ARGV[0])->slurp);

die "Invalid data: $@" unless $data;

my $args = ($ARGV[1] ? eval '+'.$ARGV[1] : {});

die "Invalid args: $@" unless $args;

my $ddc = Data::Dumper::Compact->new($args);

my @select_clauses = qw(with with_recursive _ select from where order_by group_by having);

my %select_clauses; @select_clauses{@select_clauses} = -@select_clauses..-1;

sub key_sorter (&) {
  my ($sorter) = @_;
  +{ hash => sub {
      my ($keys, $map) = @{$_[2]};
      return [ hash => [ [ $sorter->(@$keys) ], $map ] ];
  } }
}

$ddc->add_transform(
  [ -select => key_sorter { nsort_by { $select_clauses{$_}||0 } @_ } ],
)->add_transform(
  [ qr/^with/, sub { ($_ % 2) == 0 }, { array => sub {
    #my ($self, $type, $payload, $path) = @_;
    my ($first, @rest) = @{$_[2]};
    return [ array => [ [ key => $first->[1] ], [ list => \@rest ] ] ];
  } } ],
);
    
print $ddc->dump($data);
